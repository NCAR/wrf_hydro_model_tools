;----------------------------------------------------------------------
; NLDAS2WRFHYDRO_forcing_regridder_ESMFregrid.ncl
;
;
; Project : STEP
;
; Purpose : This is a ncl program to perform remapping of various NLDAS 
;           data fields to WRF-Hydro domain (for STEP).
;
;           Before running this program, run ESMF_genWgts_NLDAS2WRFHYDRO_forcing.ncl
;           to generate weights for conserve and bilinear interpolation to
;           regrid source data over destination domain.
;           Then this program reads wgtFileName_conserve and wgtFileName_bilinear
;           generated by ESMF_genWgts_WRFHYDRO_forcing.ncl. 
;
; Remarks : (1) Assumes that source files are in hourly interval. (-- important for
;               computing rainrate.
;           (2) ESMF regridding performed with "conserve" option for precipitation
;               and SWE. All other variables use bilinear option for remapping. 
;           (3) NLDAS source files do not contain "time" data field. This program will
;               parse time info from the filename and add it to the final netCDF file.
;           (4) NLDAS A_PCP is hourly precipitation amount. No need for zero'ing it out
;               at the begining of the time period.
;
;
; Usage   : ncl 'srcfilename="NLDAS_FORA0125_H.*.nc"' 'dstGridName="geo_em.d01.nc.4mile_1km.nc"' NLDAS2WRFHYDRO_forcing_regridder_ESMFregrid.ncl
;
;           srcfilename = filename pattern of the souce NLDAS files.
;                         e.g., "NLDAS_FORA0125_H.2013091300.nc", "NLDAS_FORA0125_H.2013091[2-5]00.nc" , ...
; 
; Kyoko Ikeda 23 December 2013
;
;----------------------------------------------------------------------
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_code.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_csm.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/contributed.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/contrib/ut_string.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/esmf/ESMF_regridding.ncl"

begin

;----------------------------------------------------------------------
; User settings
;----------------------------------------------------------------------

  ;---------------------------------------------------------------;
  ; Set source and destination grid filenames.                    ;
  ;---------------------------------------------------------------;
  dirm    = "./input_files/"       ; directory where source forcing data resides
  outdir  = "./output_files/"      ; directory where regridded forcing data will be placed. set to dirm for overwriting the original file

  ;---------------------------------------------------------------;
  ; Source model data tination grid filenames.                    ;
  ;---------------------------------------------------------------;
  dt=3600.0   ;forcing data timestep in seconds...

  ;---------------------------------------------------------------;
  ; Source, destination , and weight filenames for generating     ;
  ; ESMF_regridding weights                                       ;
  ;---------------------------------------------------------------;
  wgtFileName_conserve = "./NLDAS_to_WRFHYDRO_weight_conserve.nc"
  wgtFileName_bilinear = "./NLDAS_to_WRFHYDRO_weight_bilinear.nc"

  ;---------------------------------------------------------------;
  ; Data field names from the source file to be regridded         ;
  ;---------------------------------------------------------------;
   P_varname = "PRES_110_SFC"
   T_varname = "TMP_110_HTGL"
   U_varname = "U_GRD_110_HTGL"
   V_varname = "V_GRD_110_HTGL"
   Q_varname = "SPF_H_110_HTGL"
   PCP_varname  = "A_PCP_110_SFC_acc1h"
   DSWR_varname = "DSWRF_110_SFC"
   DLWR_varname = "DLWRF_110_SFC"

;----------------------------------------------------------------------
; read in source and destination grid netCDF files
;----------------------------------------------------------------------

  if ( .not.isfilepresent( dstGridName ) ) then
     print( " ... source grid file not found : "+ dstGridName )
     exit
  else 
;      dstGridName = "./geo_em.d01.nc.4mile_1km.nc"
  end if

;---  destination grid data
  dstfile     = addfile( dstGridName ,"r")
  dlon2d=dstfile->XLONG_M(0,:,:)
  dlat2d=dstfile->XLAT_M(0,:,:)
  dims=dimsizes(dlat2d)

  flag=0    ;WRF - flag for removing accum precip...

;----------------------------------------------------------------------
; Open source data files to be regridded...
;----------------------------------------------------------------------
  system("mkdir "+outdir)

  datfils = systemfunc ("ls "+dirm+srcfilename)    ;list of file names
  num_datfils     = dimsizes(datfils)

  do ifil = 0,num_datfils-1,1   ; loop through datafiles one at a time
   
      suffix = isStrSubset( datfils(ifil), ".nc" ) ; check if the input filename has .nc suffix
      if (suffix) then
         datfile = addfile( datfils(ifil), "r")
      else
      print( suffix)
         datfile = addfile( datfils(ifil)+".nc", "r")
      end if

      print( " ... Open input file : "+ datfils(ifil) )
   
     ;----------------------------------------------------------------------
     ; Parse time from filename : NLDAS_FORA0125_H.2013091300.nc
     ;----------------------------------------------------------------------
      strs = str_split( datfils(ifil), "." )
      date_str = strs(1)
      dtime_map = (/4,2,2,2/)
      date_vec = str_split_by_length( date_str, dtime_map)
      print( date_vec )

      Times_str = date_vec(0)+"-"+date_vec(1)+"-"+date_vec(2)+"_"+date_vec(3)+":00:00"
      new_string = str_strip(Times_str)
      Times = stringtochar( new_string )
      Times!0 = "DateStrLen"

      valid_time = cd_inv_calendar(toint(date_vec(0)), toint(date_vec(1)), toint(date_vec(2)), toint(date_vec(3)), toint(0) ,toint(0) ,"seconds since 1970-01-01 00:00:00", 0)
      valid_time!0 = "Time"

     ;----------------------------------------------------------------------
     ; Open output file
     ;----------------------------------------------------------------------
      ncfile= outdir + date_str+".LDASIN_DOMAIN2.nc"
      if ( isfilepresent( ncfile ) ) then
         system ("/bin/rm "+ncfile)
      end if
      ncdf= addfile(ncfile,"c")
      filedimdef( ncdf ,"Time",-1,True)
      ncdf->lat = dlat2d   ;output lat
      ncdf->lon = dlon2d   ;output lon
      ncdf->Times = Times  ;output times
      ncdf->valid_time = valid_time
   
     ;----------------------------------------------------------------------
     ;  Processing...no further mods should be required...
     ;----------------------------------------------------------------------
     do v=1,8
     
        if (v.eq.1) then
           var = datfile->$T_varname$
;           wgtFileName = wgtFileName_bilinear
           wgtFileName = wgtFileName_conserve
        end if
        if (v.eq.2) then
           var = datfile->$Q_varname$
;           wgtFileName = wgtFileName_bilinear
           wgtFileName = wgtFileName_conserve
        end if
        if (v.eq.3) then
           var = datfile->$U_varname$
;           wgtFileName = wgtFileName_bilinear
           wgtFileName = wgtFileName_conserve
        end if
        if (v.eq.4) then
           var = datfile->$V_varname$
;           wgtFileName = wgtFileName_bilinear
           wgtFileName = wgtFileName_conserve
        end if
        if (v.eq.5) then
           var = datfile->$P_varname$
;           wgtFileName = wgtFileName_bilinear
           wgtFileName = wgtFileName_conserve
        end if
        if (v.eq.6) then
          ;wgtFileName = wgtFileName_bilinear
           wgtFileName = wgtFileName_conserve
           var = datfile->$PCP_varname$ 
           var = var / dt   ;       convert from mm to mm/s
           var@long_name = "RAINRATE"
           var@description = "RAINRATE"
           var@units       = "mm s^-1"
        end if
        if (v.eq.7) then
           var = datfile->$DSWR_varname$
;           wgtFileName = wgtFileName_bilinear
           wgtFileName = wgtFileName_conserve
        end if
        if (v.eq.8) then
           var = datfile->$DLWR_varname$
;           wgtFileName = wgtFileName_bilinear
           wgtFileName = wgtFileName_conserve
        end if
     
       printVarSummary(var)
     
       ;----------------------------------------------------------------------
       ; Define new array and perform regriddding
       ;----------------------------------------------------------------------
        ntimes = 1  ; for NLDAS, there is only one time step in each file
        v4 = new((/ntimes(0), dims(0), dims(1)/),"double")
       
       printVarSummary(v4)

       ;---Options to pass to ESMF_regrid--------------------;
     
        opt                = True
        opt@WgtFileName    = wgtFileName
        opt@CopyVarAtts    = True
        opt@CopyVarCoords  = False
     
       ;---Debug information
         opt@PrintTimings   = True
         opt@Debug          = True
       ;-----------------------------------------------------;
        exist = isfilepresent( wgtFileName )
        if ( .not.exist ) then
           print( " ... no wgtFile. Run ESMF_genWgts_NLDAS2WRFHYDRO_forcing.ncl first."+" "+wgtFileName )
           exit
        end if
     
        var2d = var
        v4 = ESMF_regrid_with_weights( var2d , wgtFileName, opt)
        ;va = ESMF_regrid_with_weights( var2d , wgtFileName, opt)

       ; printVarSummary(va)
       ; exit
     
       ;----------------------------------------------------------------------
       ; Export interpolated data to new forcing file...
       ;----------------------------------------------------------------------
       
       ;--- change dimension names so that ncks command works appropriately ---;
       v4!0 = "Time"
       v4!1 = "south_north"
       v4!2 = "west_east"
       printVarSummary(v4)
       
       if (v.eq.1) then
              ncdf->T2D = v4
       else if (v.eq.2) then
              ncdf->Q2D = v4
       else if (v.eq.3) then
              ncdf->U2D = v4
       else if (v.eq.4) then
              ncdf->V2D = v4
       else if (v.eq.5) then
              ncdf->PSFC = v4
       else if (v.eq.6) then
              ncdf->RAINRATE = v4
       else if (v.eq.7) then
              ncdf->SWDOWN = v4
       else if (v.eq.8) then
              ncdf->LWDOWN = v4
       end if
       end if
       end if
       end if
       end if
       end if
       end if
       end if
       delete([/var,v4,var2d,wgtFileName/])
     
     end do   ; end do for variable loop
   
     dstFile = str_sub_str(ncfile, ".nc" , "")
     system( "mv "+ncfile+" "+dstFile)
   end do   ; end do for file loop


end
